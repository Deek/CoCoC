***************************************************************
* 'movemem.a' - memory moving functions for OS-9/6809.
* by Mike Sweet 3/21/90
*
* movemem(d,s,bytes)
* char *d,*s;
* int bytes;
*
* 'movemem()' wil copy the data at 's' to 'd', taking
* into account any overlap that might exist.
*

 psect movemem,0,0,0,6,0

movemem: pshs y,u
 ldx 6,s
 ldy 8,s
 ldu 10,s
 cmpx 8,s is the destination > source?
 bhi movehigh

movelow cmpu #2
 blo movelst1
 ldd ,y++
 std ,x++
 leau -2,u
 bra movelow
movelst1 cmpu #1
 bne moverts
 lda ,y
 sta ,x
moverts puls y,u,pc

movehigh tfr u,d
 leay d,y
 leax d,x

mloop1 cmpu #2
 blo movelst2
 ldd ,--y
 std ,--x
 leau -2,u
 bra mloop1
movelst2 cmpu #1
 bne moverts
 lda ,-y
 sta ,-x
 puls y,u,pc

 endsect
 
