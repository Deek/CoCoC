*****************************************
* Drawing Functions                     *
* Copyright (c) 1989 by Mike Sweet      *
*****************************************

 ifp1
 use /dd/defs/os9defs.a
 endc

 psect draw,0,0,0,0,0

SetDPtr:
 ldd #$1b40
 bra send6

RSetDPtr:
 ldd #$1b41
 bra send6

Point:
 ldd #$1b42
 bra send6

RPoint:
 ldd #$1b43
 bra send6

Line:
 ldd #$1b44
 bra send6

RLine:
 ldd #$1b45
 bra send6

LineM:
 ldd #$1b46
 bra send6

RLineM:
 ldd #$1b47
 bra send6

Box:
 ldd #$1b48
 bra send6

RBox:
 ldd #$1b49
 bra send6

Bar:
 ldd #$1b4a
 bra send6

RBar:
 ldd #$1b4b
 bra send6

FFill:
 ldd #$1b4f

send2 pshs u,d
 lda 7,s
 leax ,s
 ldu #2
 lbsr _write
 leas 2,s
 bra os9err0

FCircle:
 pshs u
 ldd 6,s
 pshs d
 ldd #$1b53
 bra _docirc
Circle:
 pshs u
 ldd 6,s
 pshs d
 ldd #$1b50
_docirc
 pshs d
 lda 9,s
 leax ,s
 ldu #4
 lbsr _write
 leas 4,s
 bra os9err0

Ellipse:
 ldd #$1b51
 bra send6

FEllipse:
 ldd #$1b54
 bra send6

PutGC:
 ldd #$1b4e

send6 pshs u
 leas -6,s
 std ,s
 ldd 12,s
 std 2,s
 ldd 14,s
 std 4,s
 lda 11,s
 leax ,s
 ldu #6
 lbsr _write
 leas 6,s

os9err0 puls u
 bcc noerr0
 clra 
 std errno,y
 ldd #-1
 rts
noerr0 clra
 clrb
 rts

Arc:
 pshs u
 leax 4,s
 ldu #$1b52
 lda 5,s
 stu ,x
 ldu #14
 lbsr _write
 bra os9err0

 endsect
