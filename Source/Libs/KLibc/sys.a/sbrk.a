*
* Request memory from OS
*
 use /dd/defs/os9defs.a
 psect sbrk,0,0,6,0,0

* sbrk(size)   get memory from system
sbrk: ldd memend,y get hi bound
 pshs d save it
 ldd 4,s get the size required
 cmpd _spare,y any spare left?
 blo sbrk20

* have to get some from the system
 pshs y save data pointer
 clra
 clrb
 os9 F$Mem get current size/end
 addd 6,s add in additional
 os9 F$Mem re-size memory
 tfr y,d save high bound
 puls y restore data ptr
 bcc sbrk10 bra if no error
 leas 2,s junk scratch
 ldb #E$MemFul out of memory
 lbra _os9err return error

sbrk10 std memend,y save new memory address
 addd _spare,y add in spare bytes
 subd 0,s less old base
 std _spare,y is new spare value

* now spare is big enough
sbrk20 leas 2,s junk scratch
 ldd _spare,y get spare count
 pshs d
 subd 4,s less size
 std _spare,y updated value
 ldd memend,y get hi bound
 subd ,s++ base of free memory
 pshs d save

 clra
 ldx 0,s
sbrk30 sta ,x+ clear out the new memory
 cmpx memend,y
 blo sbrk30

 puls d,pc

 endsect
