 psect strtok_a,0,0,1,0,0

 vsect
save fdb 0 safe string ptr
 endsect

*
* char  *
* strtok(string, sepset)
* char  *string, *sepset;
*    {
*    register char  *p;
*    static   char  *save;

strtok:
 clra
 clrb
 pshs D,U save U and get scratch
*
*    if ((p = (string == NULL) ? save : string) == NULL)
*       return (NULL);                            /* no tokens remain */
 ldu 6,S get *string
 bne strtok1 if first call
 ldu save,Y get saved version
 beq nomore if none left
*
*    p += strspn(p, sepset);               /* skip leading seperators */
strtok1 ldx 8,S get *sepset
 pshs X set for call
 pshs U
 lbsr strspn
 leas 4,S
 leau D,U
*
*    if (*p == '\0')
*       return (NULL);                            /* no tokens remain */
 ldb ,U check for end of string
 beq nomore
 stu ,S finally something to return
*
*    if ((save = strpbrk(p, sepset)) != NULL)  /* find next seperator */
*       *save++ = '\0';                       /* terminate this token */
 ldx 8,S
 pshs X set for call
 pshs U
 lbsr strpbrk
 leas 4,s
 std save,Y update save past end
 beq nomore
 tfr D,X
 clr ,X+
 stx save,Y
*
*    return (p);
*    }
nomore puls D,U,PC

 endsect
